---
# generated by https://github.com/hashicorp/terraform-plugin-docs
page_title: "bytebase_policy Resource - terraform-provider-bytebase"
subcategory: ""
description: |-
  The policy resource.
---

# bytebase_policy (Resource)

The policy resource. You can read, create, update or delete a single instance policy `bytebase_policy` resource.

## Example Usage

```terraform
# Create backup plan policy for test env.
resource "bytebase_policy" "backup_plan" {
  environment = test
  type        = "BACKUP_PLAN"

  backup_plan_policy {
    schedule           = "WEEKLY"
    retention_duration = 86400
  }
}
```

You can check [examples](https://github.com/bytebase/terraform-provider-bytebase/blob/main/examples/setup) for more usage examples.

<!-- schema generated by tfplugindocs -->

## Schema

### Required

#### The policy type

- `type` (String) The policy type. Should be one of:
  - `DEPLOYMENT_APPROVAL`
  - `BACKUP_PLAN`
  - `SENSITIVE_DATA`
  - `ACCESS_CONTROL`
  - `SQL_REVIEW`

#### The policy payload

According to the policy `type`, you need to specific the policy payload, which should be one of:

##### Deployment Approval Policy

Must set the `deployment_approval_policy` if the policy type is `DEPLOYMENT_APPROVAL`. It contains following attributes:

- `default_strategy` (String) The default strategy, should be one of:
  - `MANUAL`: The pipeline should be manually approved by user to proceed.
  - `AUTOMATIC`: The pipeline will automatically be approved without user intervention.
- `deployment_approval_strategies` (List of Object) A list contains multiply strategies for different approval groups. The object should contains:
  - `approval_group` (String) The approval group, should be one of:
    - `APPROVAL_GROUP_DBA`: Means the assignee can be selected from the workspace owners and DBAs.
    - `APPROVAL_GROUP_PROJECT_OWNER`: Means the assignee can be selected from the project owners.
  - `approval_strategy` (String) The approval strategy, should be one of:
    - `MANUAL`: The pipeline should be manually approved by user to proceed.
    - `AUTOMATIC`: The pipeline will automatically be approved without user intervention.
  - `deployment_type` (String) The deployment type, should be one of:
    - `DATABASE_CREATE`: The deployment type for creating databases.
    - `DATABASE_DDL`: The deployment type for updating database schemas (DDL).
    - `DATABASE_DDL_GHOST`: The deployment type for updating database schemas using gh-ost.
    - `DATABASE_DML`: The deployment type for updating database data (DML).
    - `DATABASE_RESTORE_PITR`: The deployment type for performing a Point-in-time Recovery.
    - `DATABASE_DML_ROLLBACK`: The deployment type for a generated rollback issue.

For example:

```terraform
resource "bytebase_policy" "deployment_approval" {
  type = "DEPLOYMENT_APPROVAL"

  deployment_approval_policy {
    default_strategy = "AUTOMATIC"

    deployment_approval_strategies {
      approval_group    = "APPROVAL_GROUP_DBA"
      approval_strategy = "AUTOMATIC"
      deployment_type   = "DATABASE_CREATE"
    }
    deployment_approval_strategies {
      approval_group    = "APPROVAL_GROUP_PROJECT_OWNER"
      approval_strategy = "MANUAL"
      deployment_type   = "DATABASE_DDL"
    }
  }
}
```

##### Backup Plan Policy

Backup Plan Policy is the policy configuration for backup plan.

Must set the `backup_plan_policy` if the policy type if `BACKUP_PLAN`. It contains following attributes:

- `schedule` (String) The schedule for backup plan policy. Should be one of:
  - `UNSET`: NEVER backup plan policy.
  - `DAILY`: Daily backup.
  - `WEEKLY`: Weekly backup.
- `retention_duration` (Number) The minimum allowed seconds that backup data is kept for databases in an environment.

For example:

```terraform
resource "bytebase_policy" "backup_plan" {
  type = "BACKUP_PLAN"

  backup_plan_policy {
    schedule           = "WEEKLY"
    retention_duration = 86400
  }
}
```

##### Sensitive Data Policy

Sensitive Data Policy is the policy configuration for sensitive data. It is only applicable to database resource type.

Must set the `sensitive_data_policy` if the policy type if `SENSITIVE_DATA`. It contains following attributes:

- `sensitive_data` (List of Object)
  - `schema`: The database schema.
  - `table`: The database table.
  - `column`: The column in table.
  - `mask_type`: The sensitive data type to hide data with a default method. Should be one of:
    - `DEFAULT`: The default method is subject to change.

For example:

```terraform
resource "bytebase_policy" "sensitive_data" {
  type = "SENSITIVE_DATA"
  environment = "<environment resource id>"
  instance    = "<instance resource id>"
  database    = "<database name>"

  sensitive_data_policy {
    sensitive_data {
      mask_type = "DEFAULT"
      table = "<table name>"
    }
  }
}
```

##### Access Control Policy

Access Control Policy is the policy configuration for database access control. It is only applicable to database and environment resource type.

- For environment resource type, if the `environment_tier_policy` is set to be `PROTECTED`, the `access_control_policy` is the disallow list for databases in this environment.
- For database resource type, the access control policy means allow to access in this specific database.

Must set the `access_control_policy` if the policy type if `ACCESS_CONTROL`. It contains following attributes:

- `disallow_rules` (List of Object) The object contains following attribute:
  - `full_database` (Boolean) will apply to the full database.

For example:

```terraform
# The prod environment is marked as PROTECTED
resource "bytebase_environment" "prod" {
  resource_id             = "prod"
  title                   = "Prod"
  order                   = 1
  environment_tier_policy = "PROTECTED"
}

# Disallow to access in prod
resource "bytebase_policy" "access_control" {
  type = "ACCESS_CONTROL"
  environment = bytebase_environment.prod.resource_id

  access_control_policy {
    disallow_rules {
      full_database = true
    }
  }
}

# Allow to access in employee database in prod environment.
resource "bytebase_policy" "access_control" {
  type = "ACCESS_CONTROL"
  environment = bytebase_environment.prod.resource_id
  instance    = "<instance resource id for the database>"
  database    = "employee"

  access_control_policy {
    disallow_rules {
      full_database = true
    }
  }
}
```

##### SQL Review Policy

SQL Review Policy is the policy for SQL review.

Must set the `sql_review_policy` if the policy type if `SQL_REVIEW`. It contains following attributes:

- `title` (String) The title for SQL review.
- `rules` (List of Object) List of SQL review rules. The rule should a object contains:
  - `type` (String) The SQL review rule type. You can check the [code](https://github.com/bytebase/terraform-provider-bytebase/blob/main/api/sql_review.go) to find supported rules.
  - `level` (String) The SQL review rule level. Should be one of:
    - `ERROR`
    - `WARNING`
    - `DISABLED`
  - `payload` (Object) The payload for SQL review rule.

Please check the doc for details: https://www.bytebase.com/docs/sql-review/review-rules/supported-rules

For example:

```terraform
resource "bytebase_policy" "sql_review" {
  type = "SQL_REVIEW"
  environment = "<environment resource id>"

  sql_review_policy {
    title = "SQL Review Policy for Test environment"
    rules {
      type  = "statement.select.no-select-all"
      level = "ERROR"
    }
    rules {
      type  = "naming.table"
      level = "ERROR"
      payload {
        max_length = 99
        format     = "^[a-z]+$"
      }
    }
    rules {
      type  = "column.required"
      level = "WARNING"
      payload {
        list = ["id", "created_ts", "updated_ts"]
      }
    }
    rules {
      type  = "column.auto-increment-initial-value"
      level = "DISABLED"
      payload {
        number = 1
      }
    }
  }
}
```

###### Naming format rules

Rules to limit the naming format:

- `naming.table`
- `naming.column`
- `naming.column.auto-increment`
- `naming.index.fk`
- `naming.index.idx`
- `naming.index.uk`

For naming format rules, we need to set the `payload` with `max_length` and `format` attributes. For example:

```terraform
resource "bytebase_policy" "sql_review" {
  type = "SQL_REVIEW"
  environment = "<environment resource id>"

  sql_review_policy {
    title = "SQL Review Policy for Test environment"
    rules {
      type  = "naming.table"
      level = "ERROR"
      payload {
        max_length = 99
        format     = "^[a-z]+$"
      }
    }
  }
}
```

###### Comment format rules

Rules to limit the comment for table and column:

- `column.comment`
- `table.comment`

For comment format rules, we need to set the `payload` with `max_length` and `required` attributes. For example:

```terraform
resource "bytebase_policy" "sql_review" {
  type = "SQL_REVIEW"
  environment = "<environment resource id>"

  sql_review_policy {
    title = "SQL Review Policy for Test environment"
    rules {
      type  = "column.comment"
      level = "WARNING"
      payload {
        max_length = 99
        required   = true
      }
    }
  }
}
```

###### Number limit rules

Rules to limit by a specific number.

- `index.key-number-limit`
- `index.total-number-limit`
- `statement.insert.row-limit`
- `statement.affected-row-limit`
- `column.maximum-character-length`
- `column.auto-increment-initial-value`

For number limit rules, we need to set the `payload` with `number` attribute. For example:

```terraform
resource "bytebase_policy" "sql_review" {
  type = "SQL_REVIEW"
  environment = "<environment resource id>"

  sql_review_policy {
    title = "SQL Review Policy for Test environment"
    rules {
      type  = "column.auto-increment-initial-value"
      level = "WARNING"
      payload {
        number = 1
      }
    }
  }
}
```

###### List limit rules

Rules define the allow or disallow list.

- `column.required`
- `column.type-disallow-list`
- `system.charset.allowlist`
- `system.collation.allowlist`
- `index.primary-key-type-allowlist`

For list limit rules, we need to set the `payload` with `list` attribute. For example:

```terraform
resource "bytebase_policy" "sql_review" {
  type = "SQL_REVIEW"
  environment = "<environment resource id>"

  sql_review_policy {
    title = "SQL Review Policy for Test environment"
    rules {
      type  = "column.required"
      level = "WARNING"
      payload {
        list = ["id", "created_ts", "updated_ts"]
      }
    }
  }
}
```

### Optional

#### Locate the policy resource

We can use following parameters to locate the policy.

- `project`: The project resource id. You can use this to CRUD the policy in a specific project.
- `environment`: The environment resource id. You can use this to CRUD the policy in a specific environment.
- `instance`: The instance resource id. You can use this to CRUD the policy in a specific instance. It must works with the `environment`.
- `database`: The database name. You can use this to CRUD the policy in a specific database. It must works with the `environment` and the `instance`.

For example:

```terraform
# Create the policy in the whole workspace:
resource "bytebase_policy" "backup_plan" {
  type = "BACKUP_PLAN"

  backup_plan_policy {
    schedule           = "WEEKLY"
    retention_duration = 86400
  }
}

# Create the policy in a project:
resource "bytebase_policy" "backup_plan" {
  project = "<project resource id>"
  type    = "BACKUP_PLAN"

  backup_plan_policy {
    schedule           = "WEEKLY"
    retention_duration = 86400
  }
}

# Create the policy in a env.
resource "bytebase_policy" "backup_plan" {
  environment = "<environment resource id>"
  type        = "BACKUP_PLAN"

  backup_plan_policy {
    schedule           = "WEEKLY"
    retention_duration = 86400
  }
}

# Create the policy in a instance under an env.
resource "bytebase_policy" "backup_plan" {
  environment = "<environment resource id>"
  instance    = "<instance resource id>"
  type        = "BACKUP_PLAN"

  backup_plan_policy {
    schedule           = "WEEKLY"
    retention_duration = 86400
  }
}

# Create the policy in a database under an instance.
resource "bytebase_policy" "backup_plan" {
  environment = "<environment resource id>"
  instance    = "<instance resource id>"
  database    = "<database name>"
  type        = "BACKUP_PLAN"

  backup_plan_policy {
    schedule           = "WEEKLY"
    retention_duration = 86400
  }
}
```

#### Policy attribute

- `inherit_from_parent`: Decide if the policy should inherit from the parent.
